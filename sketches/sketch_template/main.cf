body file control
{
  namespace => "cfdc_sketch_template";
}

bundle common runenv(runenv)
{
  vars:
      "vars" slist => splitstring("$(default:$(runenv).env_vars_str)", " ", 999); # unpack the list of runenv scalars
      # "vars" slist => { "@(default:$(runenv).env_vars)" }; # we'll use this when we depend on 3.5.x because of a 3.4.x bug with slist iteration between bundles
      "$(vars)" string => "$(default:$(runenv).$(vars))";

  classes:
      "$(vars)" expression => "default:runenv_$(runenv)_$(vars)";

  reports:
    verbose::
      "Imported var $(vars) with value $($(vars))";
}

bundle agent entry(runenv, metadata, prefix, myboolean, mytype, myip, mylist, myarray)
{
  methods:
      "runenv" usebundle => cfdc_sketch_template:runenv($(runenv));

  classes:
      # note that demo.json uses bycontext to set mytype to "alpha"
      # when context A is defined, so context A indirectly triggers
      # use_alpha
      "use_alpha"     expression => strcmp($(mytype), "alpha");

      # Note that if you don't make the "mymodule" execution
      # conditional, cf-promises will fail to validate main.cf the
      # FIRST time, BEFORE "mymodule" is installed in
      # $(sys.workdir)/modules.
      "exists_module" expression => fileexists("$(sys.workdir)/modules/mymodule");

      # call the "mymodule" module
    exists_module::
      "ran_module" expression => usemodule("mymodule", "");

  vars:
      # often you need a unique value for the current run through a
      # bundle, especially if you use if_repaired and such.  The
      # canon_prefix will give you that.
      "canon_prefix" string => canonify("$(prefix)");

      # call the sample.sh script
      "sample_return" string => execresult("$(bundle_home)/scripts/sample.sh",
                                           "useshell");

      # pretty-print the authors and dependencies
      "dependencies" string => join(", ", "$($(metadata)[depends])");
      "authors" string => join(", ", "$($(metadata)[authors])");

  reports:
    verbose::
      "$($(metadata)[name]) license = $($(metadata)[license])";
      "$($(metadata)[name]) dependencies = $(dependencies)";

      "$($(metadata)[name]) version $($(metadata)[version]) by $(authors) starting up...";

    cfengine::
      # This will define localclass with a locally meaningful prefix
      # every time (otherwise, localclass will be global and will be
      # defined with the same name every time).
      "Hello there"
      classes => default:if_repaired("$(canon_prefix)localclass");

      # And this will use the localclass
      "Hello back"
      ifvarclass => "$(canon_prefix)localclass";

      # use the "verbose" context to display extra information (for the user)
    verbose::
      "Invoking entry_point: prefix $(prefix) type $(mytype) myarray lookup of path1 $($(myarray)[path1])";

      # use the "test" context to help debug the sketch (for the developer)
    test::
      "This is debugging";
}
