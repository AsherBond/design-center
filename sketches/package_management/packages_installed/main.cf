body file control
{
      namespace => "cfdc_packages";
}

bundle agent installed(runenv, metadata, pkgs_add, state)
{
#@include "REPO/sketch_template/standard.inc"

  methods:
    not_test::
      "install" usebundle => default:cfe_package_ensure($(pkgs_add), $(state));

  reports:
    test::
      "$(dcbundle): In test mode, simulating adding package $(pkgs_add)";
}

bundle agent from_file(runenv, metadata, file, state)
{
#@include "REPO/sketch_template/standard.inc"
  vars:
      "todo" slist => readstringlist($(file), "#.*", "\n", 10000, 500000);

  classes:
      "have_todo" expression => isvariable("todo");

  methods:
    have_todo.not_test::
      "install" usebundle => default:cfe_package_ensure($(todo), $(state));

  reports:
    have_todo.test::
      "$(dcbundle): In test mode, simulating adding package $(todo)";
    !have_todo::
      "$(dcbundle): file $(file) could not be parsed to get the file list";
}

bundle agent from_url(runenv, metadata, url, state, retriever, spooldir)
{
#@include "REPO/sketch_template/standard.inc"

  vars:
      "basename" string => lastnode($(url), "/");
      "spoolfile" string => "$(spooldir)/$(basename)";

  classes:
      "have_file" expression => fileexists($(spoolfile));

  commands:
    have_exec_prefix::
      "$(exec_prefix)$(retriever) > $(spoolfile)"
      contain => default:in_shell,
      classes => default:scoped_classes_generic("retrieve", "bundle");

  methods:
    not_test.have_file::
      "install" usebundle => default:cfe_package_ensure_named($(spoolfile), $(state), ">=", "0.0.0", "*");

  reports:
    have_file.test::
      "$(dcbundle): In test mode, simulating adding package $(spoolfile)";
    !have_file.retrieve_reached::
      "$(dcbundle): file $(spoolfile) could not be retrieved from $(url)";
}
