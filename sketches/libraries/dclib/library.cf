bundle agent report_metadata(bundlename, metadata)
{
  vars:
      # pretty-print the authors and dependencies
      "dependencies" string => join(", ", "$(metadata)[depends]");
      "authors" string => join(", ", "$(metadata)[authors]");

  reports:
      "$(bundlename): $($(metadata)[name]) license = $($(metadata)[license])";
      "$(bundlename): $($(metadata)[name]) dependencies = $(dependencies)";

      "$(bundlename): $($(metadata)[name]) version $($(metadata)[version]) by $(authors) starting up...";
}

bundle edit_line converge(marker, lines)
{
  vars:
      "regex" string => escape($(marker));

  delete_lines:
      "$(regex)" comment => "Delete lines matching the marker";
  insert_lines:
      "$(lines)" comment => "Insert the given lines";
}

bundle common env
{
  vars:
      "env" slist => { "LOGNAME", "PATH", "HOME" };
      "$(env)" string => getenv("$(env)", "128");
}
